plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '3.0.1'
}

group 'fr.unepicier'
version '1.0'
project.description='Finance Tracker JAVA App'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.0'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'fr.unepicier.financetracker'
    mainClass = 'fr.unepicier.financetracker.FinanceTrackerApplication'
}

javafx {
    version = '21'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
    implementation("org.xerial:sqlite-jdbc:3.45.1.0")
    implementation("org.apache.logging.log4j:log4j-core:2.22.1")
    implementation("org.slf4j:slf4j-log4j12:2.0.11")
    implementation("org.slf4j:slf4j-api:2.0.11")
}

test {
    useJUnitPlatform()
}

jlink {
    addExtraDependencies('slf4j')

    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'FinanceTracker'
    }
    jpackage {
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'

        icon = "src/main/resources/fr/unepicier/financetracker/assets/FinanceTracker.$imgType"
        installerOptions += [
                '--description', project.description,
                '--app-version', version
        ]

        if (currentOs.windows) {
            installerType = 'exe'
            imageOptions += ['--icon', icon]
            installerOptions += [
                    '--win-per-user-install',
                    '--win-dir-chooser',
                    '--win-menu',
                    '--win-shortcut',
                    '--win-shortcut-prompt'
            ]
        }
        if (currentOs.linux) {
            installerType = 'deb'
            imageOptions += ['--icon', icon]
            installerOptions += ['--linux-menu-group', 'Office', '--linux-shortcut']
        }
        if (currentOs.macOsX) {
            installerType = 'pkg'
            imageOptions += ['--icon', icon]
            installerOptions += ['--mac-package-identifier', 'fr.unepicier.financetracker']
        }
    }
}

jlinkZip {
    group = 'distribution'
}